plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id("de.mannodermaus.android-junit5") version "1.9.3.0"
    id 'jacoco'
}
apply plugin: 'kotlin-kapt'
apply plugin: 'dagger.hilt.android.plugin'
apply plugin: 'com.google.gms.google-services'

// アプリのバージョンをversion.propertiesから取得
def versionProps = new Properties()
versionProps.load(project.rootProject.file('version.properties').newDataInputStream())
def majorVersion = versionProps.getProperty('version.major')
def minorVersion = versionProps.getProperty('version.minor')
// パッチバージョンはパラメータが与えられている場合はそちらを優先し、無い場合はversion.propertiesから取得する
def patchVersion = project.hasProperty('patchVersion') ? project.getProperty('patchVersion') : versionProps.getProperty('version.patch')

android {
    signingConfigs {
        release {
            def properties = new Properties()
            properties.load(project.rootProject.file("local.properties").newDataInputStream())
            storeFile file(properties.getProperty("keystore.file"))
            storePassword properties.getProperty("keystore.storepwd")
            keyAlias 'key0'
            keyPassword properties.getProperty("keystore.keypwd")
        }
    }

    compileSdk 34

    defaultConfig {
        applicationId "net.wackwack.pic_card_memory"
        minSdk 23
        targetSdk 34
        versionCode 8
        versionName "${majorVersion}.${minorVersion}.${patchVersion}"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        archivesBaseName = "app-${defaultConfig.versionName}"
    }

    lintOptions {
        baseline file("lint-baseline.xml")
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    buildFeatures {
        viewBinding true
    }
    dataBinding {
        enabled = true
    }
    namespace 'net.wackwack.pic_card_memory'


    testOptions {
        unitTests.all {
            jvmArgs '--add-opens', 'java.base/java.lang=ALL-UNNAMED'
            jvmArgs '--add-opens', 'java.base/java.util=ALL-UNNAMED'
        }
    }
}


dependencies {

    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.10.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.6.2'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.2'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.6.2'
    implementation 'androidx.fragment:fragment-ktx:1.6.2'
    implementation 'androidx.preference:preference-ktx:1.2.1'
    implementation "com.google.dagger:hilt-android:$hlt_version"
    implementation 'androidx.test.espresso:espresso-idling-resource:3.5.1'
    implementation 'com.google.firebase:firebase-auth-ktx:22.3.0'
    implementation 'androidx.test:core-ktx:1.5.0'
    kapt "com.google.dagger:hilt-android-compiler:$hlt_version"
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.3'
    testImplementation "com.nhaarman:mockito-kotlin:1.6.0"
    testImplementation 'org.mockito:mockito-core:5.5.0'
    testImplementation 'org.mockito:mockito-inline:5.2.0'
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.4"
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test:rules:1.5.0'
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'
    implementation platform('com.google.firebase:firebase-bom:30.0.2')
    implementation 'com.google.firebase:firebase-storage-ktx:20.3.0'
}