plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id("de.mannodermaus.android-junit5") version "1.13.1.0"
    id 'com.google.android.gms.oss-licenses-plugin'
    id 'jacoco'
}
apply plugin: 'kotlin-kapt'
apply plugin: 'dagger.hilt.android.plugin'
apply plugin: 'com.google.gms.google-services'

// アプリのバージョンをversion.propertiesから取得
def versionProps = new Properties()
versionProps.load(project.rootProject.file('version.properties').newDataInputStream())
def majorVersion = versionProps.getProperty('version.major')
def minorVersion = versionProps.getProperty('version.minor')
// パッチバージョンはパラメータが与えられている場合はそちらを優先し、無い場合はversion.propertiesから取得する
def patchVersion = project.hasProperty('patchVersion') ? project.getProperty('patchVersion') : versionProps.getProperty('version.patch')

android {
    signingConfigs {
        release {
            def properties = new Properties()
            properties.load(project.rootProject.file("local.properties").newDataInputStream())
            storeFile file(properties.getProperty("keystore.file"))
            storePassword properties.getProperty("keystore.storepwd")
            keyAlias 'key0'
            keyPassword properties.getProperty("keystore.keypwd")
        }
    }

    compileSdk 36

    defaultConfig {
        applicationId "net.wackwack.pic_card_memory"
        minSdk 24
        targetSdk 36
        versionCode 7
        versionName "${majorVersion}.${minorVersion}.${patchVersion}"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        archivesBaseName = "app-${defaultConfig.versionName}"
    }

    lint {
        baseline file("lint-baseline.xml")
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    buildFeatures {
        viewBinding true
        dataBinding true
    }

    namespace 'net.wackwack.pic_card_memory'

    testOptions {
        unitTests.all {
            jvmArgs '--add-opens', 'java.base/java.lang=ALL-UNNAMED'
            jvmArgs '--add-opens', 'java.base/java.util=ALL-UNNAMED'
        }
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.15.0'
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.8.7'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.8.7'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.8.7'
    implementation 'androidx.fragment:fragment-ktx:1.8.5'
    implementation 'androidx.preference:preference:1.2.1'
    implementation "com.google.dagger:hilt-android:$hlt_version"
    implementation 'androidx.test.espresso:espresso-idling-resource:3.6.1'

    implementation 'com.google.firebase:firebase-auth-ktx:23.0.0'
    implementation 'com.google.android.gms:play-services-oss-licenses:17.1.0'
    kapt "com.google.dagger:hilt-android-compiler:$hlt_version"

    testImplementation 'org.junit.jupiter:junit-jupiter:5.11.3'
    testImplementation "org.mockito.kotlin:mockito-kotlin:5.4.0"
    testImplementation 'org.mockito:mockito-core:5.14.2'
    testImplementation 'org.mockito:mockito-inline:5.2.0'

    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
    androidTestImplementation 'androidx.test:runner:1.6.2'
    androidTestImplementation 'androidx.test:rules:1.6.1'
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.3.0'
    implementation platform('com.google.firebase:firebase-bom:33.4.0')
    implementation 'com.google.firebase:firebase-storage-ktx:21.0.1'
}
